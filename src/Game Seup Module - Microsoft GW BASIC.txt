10000 REM *****************
10010 REM *  STAR TRADER  *
10020 REM * SET UP MODULE *
10030 REM *****************
10040 REM
10050 REM ******************
10060 REM * VARIABLE TABLE *
10070 REM ******************
10080 REM
10090 DIM A$[6] : REM IS THE STANDARD INPUT BUFFER
10100 DIM SHIPNAME$[12] : REM TRADING SHIP NAME STRING
10110 DIM TONNAGE : REM MAX WEIGHT OF A TRADING SHIP
10120 DIM MINDIST : REM MINIMUM DISTANCE ALLOWED BETWEEN STARS
10130 DIM PROFITMARGIN : REM CONTROLS THE PROFIT MARGIN; HIGH 
PROFITMARGIN LIMITS THE %
10140 DIM SHIPSPEED : REM SPEED OF A SHIP IN LIGHT-YEARS PER DAY
10150 DIM DEVELOPINCREMENT : REM IS THE STELLAR DEVELOPMENT INCREMENT 
(1<=DEVELOPINCREMENT<=5)
10160 DIM DELAYPROBABILITY : REM DELAYPROBABILITY IS THE PROBABILITY OF 
A DELAY
10170 DIM STARCLASS : REM DEVELOPMENT CLASS OF THE STAR
10180 DIM ENDYEAR : REM YEAR GAME ENDS
10190 REM
10200 REM		       M AND C DETERMINE A STAR'S PRODUCTIVITY/MONTH
10210 DIM M[6,3] : REM PROD/MO. = STAR[7,J] * M[I,STARCLASS] + 
C[I,STARCLASS]
10220 DIM C[6,3] : REM WHERE J IS THE STAR ID #, I THE MERCHANDISE #,
10230 REM              AND STARCLASS IS THE DEVELOPMENT CLASS OF THE 
STAR
10240 REM
10250 REM INFO ARRAYS
10260 REM
10270 DIM STAR[12,15] : REM STAR SYSTEM INFO ARRAY
10280 REM
10290 REM STAR SYSTEM INFO ARRAY
10300 REM      POSITION 1  = PRODUCT 1 UR
10310 REM      POSITION 2  = PRODUCT 2 MET
10320 REM      POSITION 3  = PRODUCT 3 HE
10330 REM      POSITION 4  = PRODUCT 4 MED
10340 REM      POSITION 5  = PRODUCT 5 SOFT
10350 REM      POSITION 6  = PRODUCT 6 GEMS
10360 REM      POSITION 7  = DEVELOPMENTAL NUMBER START AT 0(FRONTIER), 
5(UNDERDEVELOPED), 10(DEVELOPED), OR 15(SOL)
10370 REM      POSITION 8  = STAR NAME LOOKUP POSITION
10380 REM      POSITION 9  = DAY STAR CREATED 
10390 REM      POSITION 10 = YEAR STAR CREATED 
10400 REM      POSITION 11 = X LOCATION IN STAR MAP
10410 REM      POSITION 12 = Y LOCATION IN STAR MAP 
10420 REM
10430 DIM SHIP[12,12] : REM TRADING SHIP INFO ARRAY
10440 DIM BANK[3,12] : REM BANK ACCOUNT INFO ARRAY 
10450 REM
10460 REM
10470 REM -NUMBER OF- VARIABLES
10480 REM
10490 DIM NUMPLAYERS : REM NUMBER OF PLAYERS
10500 DIM NUMSHIPS : REM NUMBER OF TRADING SHIPS (2 * NUMPLAYERS)
10510 DIM NUMSTARS : REM NUMBER OF STAR SYSTEMS
10520 DIM NUMSHIPSPERPLAYER : REM NUMBER OF TRADING SHIPS PER PLAYER
10530 DIM NUMYEARS : REM LENGTH OF GAME IN YEARS
10540 DIM BIDROUNDS : REM MAX NUMBER OF BIDDING ROUNDS
10550 REM
10560 REM
10570 REM FLAG VARIABLES
10580 REM
10590 DIM FLAGHALFBOARD : REM WHICH HALF BOARD FLAG
10600 DIM FLAGRESTART : REM IS THIS RESTART FLAG
10610 REM
10620 REM
10630 REM ARRAY POSITIONN VARIABLES CURRENTLY BEING PROCESSED
10640 REM
10650 DIM CURRPLAYER : REM PLAYER CURRENTLY BEING PROCESSED
10660 DIM CURRSHIP : REM CURRENT TRADING SHIP BEGIN PROCESSED
10670 DIM CURRSTAR : REM CURRENT STAR BEING PROCESSED
10680 DIM CURRYEAR : REM CURRENT YEAR
10690 DIM CURRDAY : REM CURRENT DAY OF CURRENT YEAR (1<=CURRDAY<=360)
10700 REM
10710 REM FLAGRESTART = 1 IF THIS IS A RESTART
10720 RANDOMIZE TIMER
10730 REM
10740 REM ***************************
10750 REM * VARIABLE INITIALIZATION *
10760 REM ***************************
10770 REM
10780 SHIPSPEED = 2 / 7
10790 MINDIST = 15
10800 DELAYPROBABILITY = .1 
10810 BIDROUNDS = 3
10820 TONNAGE = 30
10830 PROFITMARGIN = 36
10840 DEVELOPINCREMENT = 1.25
10850 FLAGRESTART = 0
10860 CURRDAY = 1
10870 CURRYEAR = 2070
10880 REM 
10890 REM *****************************
10900 REM * SET UP ECONOMETRICS MODEL *
10910 REM *****************************
10920 REM
10930 GOSUB 14050 : REM SET UP ECONOMETRICS MODEL
10940 REM  
10950 REM ****************************
10960 REM * STAR TRADER INSTRUCTIONS *
10970 REM ****************************
10980 REM STAR TRADER INSTRUCTION BLOCK
10990 CLS : PRINT "STAR TRADER"
11000 PRINT
11010 PRINT "INSTRUCTIONS (TYPE 'Y' OR 'N' PLEASE)";
11020 INPUT A$
11030 PRINT
11040 IF LEFT$(A$,1)="N" OR LEFT$(A$,1)="n" THEN GOTO 11300
11050 CLS
11060 PRINT "     THE DATE IS JAN 1, 2070 AND INTERSTELLAR FLIGHT"
11070 REM
11080 PRINT "HAS EXISTED FOR 70 YEARS.  THERE ARE SEVERAL STAR"
11090 PRINT "SYSTEMS THAT HAVE BEEN COLONIZED.  SOME ARE ONLY"
11100 PRINT "FRONTIER SYSTEMS, OTHERS ARE OLDER AND MORE DEVELOPED."
11110 PRINT
11120 PRINT "     EACH OF YOU IS THE CAPTAIN OF TWO INTERSTELLAR"
11130 PRINT "TRADING SHIPS.  YOU WILL TRAVEL FROM STAR SYSTEM TO"
11140 PRINT "STAR SYSTEM, BUYING AND SELLING MERCHANDISE.  IF YOU"
11150 PRINT "DRIVE A GOOD BARGAIN YOU CAN MAKE LARGE PROFITS."
11160 PRINT
11170 PRINT "     AS TIME GOES  ON, EACH STAR SYSTEM WILL SLOWLY"
11180 PRINT "GROW, AND ITS NEEDS WILL CHANGE.  A STAR SYSTEM THAT"
11190 PRINT "NOW IS SELLING MUCH URANIUM AND RAW METALS CHEAPLY"
11200 PRINT "MAY NOT HAVE ENOUGH FOR EXPORT IN A FEW YEARS."
11210 PRINT
11220 PRINT "     YOUR SHIPS CAN TRAVEL ABOUT TWO LIGHTYEARS IN A"
11230 PRINT "WEEK AND CAN CARRY UP TO "; TONNAGE; " TONS OF CARGO.  
ONLY"
11240 PRINT "CLASS I AND CLASS II STAR SYSTEMS HAVE BANKS ON THEM."
11250 PRINT "THEY PAY 5% INTEREST AND ANY MONEY YOU DEPOSIT ON ONE"
11260 PRINT "PLANET IS AVAILABLE ON ANOTHER - PROVIDED THERE'S A LOCAL"
11270 PRINT "BANK."
11280 PRINT
11290 REM *** BLOCK #2 ***
11300 PRINT "HAVE ALL PLAYERS PLAYED BEFORE";
11310 INPUT A$
11320 PRINT
11330 IF LEFT$(A$,1)="Y" OR LEFT$(A$,1)="y" THEN GOTO 11350
11340 GOTO 11390
11350 PRINT "DO YOU WANT TO SET UP YOUR OWN GAME";
11360 INPUT A$
11370 PRINT
11380 IF LEFT$(A$,1)="Y" THEN 11510
11390 PRINT "HOW MANY PLAYERS";
11400 INPUT NUMPLAYERS
11410 PRINT
11420 ON NUMPLAYERS-1 GOTO 11460, 11460,11460
11430 PRINT " TWO, THREE, OR FOUR PLAYERS CAN PLAY."
11440 PRINT
11450 GOTO 11390
11460 NUMSHIPS = 2 * NUMPLAYERS : REM CALCULATE NUMBER OF TRADING SHIPS
11470 NUMSTARS = 3 * NUMPLAYERS + 1 : REM CALCULATE NUMBER OF STAR 
SYSTEMS
11480 ENDYEAR = CURRYEAR + 5 : REM END OF GAME YEAR
11490 GOTO 12470 : REM SET UP STAR MAP
11500 REM *** BLOCK #3 ***
11510 PRINT "IS THIS A RESTART";
11520 INPUT A$
11530 PRINT
11540 IF LEFT$(A$,1) = "N" THEN 11850
11550 PRINT "LOAD THE TAPE INTO THE TAPE READER.  WHEN IN TYPE A '?'"
11560 PRINT "YOU CAN FLIP THE SWITCH TO 'START' WHENEVER YOU'RE READY"
11570 PRINT
11580 INPUT SHIPNAME$
11590 INPUT TONNAGE, MINDIST, BIDROUNDS, PROFITMARGIN, CURRDAY, 
CURRYEAR
11600 INPUT NUMPLAYERS, NUMSHIPS, NUMSTARS, ENDYEAR, CURRSHIP, CURRSTAR
11610 REM
11620 REM READ IN STAR SYSTEM INFORMATION
11630 REM
11640 FOR CURRSTAR = 1 TO NUMSTARS
11650 FOR I = 1 TO 9 STEP 4
11660 INPUT STAR[I,CURRSTAR], STAR[I+1,CURRSTAR], STAR[I+2,CURRSTAR], 
STAR[I+3,CURRSTAR]
11670 NEXT I
11680 NEXT CURRSTAR
11690 REM
11700 REM READ IN TRADING SHIP INFORMATION
11710 REM
11720 FOR CURRSHIP = 1 TO NUMSHIPS
11730 FOR I = 1 TO 9 STEP 4
11740 INPUT SHIP[I,CURRSHIP], SHIP[I+1,CURRSHIP], SHIP[I+2,CURRSHIP], 
SHIP[I+3,CURRSHIP]
11750 NEXT I
11760 NEXT CURRSHIP
11770 REM
11780 REM READ IN BANK ACCOUNT INFORMATION FOR EACH PLAYER
11790 REM
11800 FOR CURRPLAYER = 1 TO NUMPLAYERS
11810 INPUT BANK[1,CURRPLAYER], BANK[2,CURRPLAYER], BANK[3,CURRPLAYER]
11820 NEXT CURRPLAYER
11830 FLAGRESTART = 1
11840 CHAIN "MAIN" ,,ALL
11850 PRINT "HOW MANY PLAYERS";
11860 INPUT NUMPLAYERS : REM NUMBER OF PLAYERS
11870 PRINT
11880 IF NUMPLAYERS >= 2 AND NUMPLAYERS <= 12 THEN GOTO 11910
11890 PRINT "2, 3, 4, ... , 12 CAN PLAY"
11900 GOTO 11850
11910 PRINT "HOW MANY SHIPS PER PLAYER";
11920 INPUT NUMSHIPSPERPLAYER : REM NUMBER OF SHIPS PER PLAYER
11930 PRINT
11940 IF NUMSHIPSPERPLAYER < 1 THEN GOTO 11910
11950 NUMSHIPS = NUMPLAYERS * NUMSHIPSPERPLAYER : REM NUMSHIPS IS THE 
TOTAL NUMBER OF SHIPS
11960 IF NUMSHIPS <= 12 THEN GOTO 12000
11970 PRINT "I CAN'T KEEP TRACK OF MORE THAN 12 SHIPS;"
11980 PRINT NUMPLAYERS;" PLAYERS TIMES ";NUMSHIPSPERPLAYER;" SHIPS 
MAKES ";NUMSHIPS
11990 GOTO 11910
12000 PRINT "HOW MANY STAR SYSTEMS";
12010 INPUT NUMSTARS : REM NUMSTARS IS THE NUMBER OF STAR SYSTEMS
12020 PRINT
12030 IF NUMSTARS >= 4 AND NUMSTARS <= 13 THEN GOTO 12060
12040 PRINT "FROM 4 TO 13 STARS"
12050 GOTO 12000
12060 PRINT "ENTER THE LENGTH OF GAME IN YEARS";
12070 INPUT NUMYEARS
12080 PRINT
12090 IF NUMYEARS >= 1 AND INT(NUMYEARS)= NUMYEARS THEN GOTO 12120 : 
REM MAKE SURE WHOLE # >= 1
12100 PRINT "CHOOSE A POSITIVE INTEGER"
12110 GOTO 12070
12120 ENDYEAR = CURRYEAR + NUMYEARS 
12130 PRINT "WHAT'S THE MAX CARGOE TONNAGE (USUALLY 30)";
12140 INPUT TONNAGE
12150 PRINT
12160 IF TONNAGE < 25 THEN GOTO 12130
12170 PRINT "WHAT'S THE MINIMUM DISTANCE BETWEEN STARS (USUALLY 15)";
12180 INPUT MINDIST
12190 PRINT
12200 IF MINDIST <=25 AND MINDIST >= 10 THEN GOTO 12230
12210 PRINT "MIN SPACING 10, MAX 25"
12220 GOTO 12170
12230 PRINT "HOW MANY BIDS OR OFFERS (USUALLY 3)";
12240 INPUT BIDROUNDS
12250 PRINT
12260 IF BIDROUNDS < 1 THEN 12230
12270 PRINT "SET THE PROFIT MARGIN (1, 2, 3, 4, OR 5) ... THE HIGHER"
12280 PRINT "THE NUMBER, THE LOWER THE PROFIT % ... USUALLY SET TO 2"
12290 PRINT "... YOUR NUMBER";
12300 INPUT PROFITMARGIN
12310 PROFITMARGIN = 18 * (ABS(PROFITMARGIN) ) : REM MIN 5 )
12320 REM *** BLOCK #4.1
12330 REM STAR SYSTEM INFO ARRAY
12340 REM      POSITION 1  = PRODUCT 1 UR
12350 REM      POSITION 2  = PRODUCT 2 MET
12360 REM      POSITION 3  = PRODUCT 3 HE
12370 REM      POSITION 4  = PRODUCT 4 MED
12380 REM      POSITION 5  = PRODUCT 5 SOFT
12390 REM      POSITION 6  = PRODUCT 6 GEMS
12400 REM      POSITION 7  = DEVELOPMENTAL NUMBER START AT 0(FRONTIER), 
5(UNDERDEVELOPED), 10(DEVELOPED), OR 15(SOL)
12410 REM      POSITION 8  = STAR NAME LOOKUP POSITION
12420 REM      POSITION 9  = DAY STAR CREATED 
12430 REM      POSITION 10 = YEAR STAR CREATED 
12440 REM      POSITION 11 = X LOCATION IN STAR MAP
12450 REM      POSITION 12 = Y LOCATION IN STAR MAP 
12460 CURRSTAR = 1 : REM SOL
12470 STAR[11, CURRSTAR] = 0 : REM X LOCATION IN STAR MAP
12480 STAR[12, CURRSTAR] = 0 : REM Y LOCATION IN STAR MAP
12490 STAR[7, CURRSTAR] = 15 : REM DEVELOPMENTAL NUMBER (15 FOR SOL)
12500 REM *** BLOCK #4.2
12510 FLAGHALFBOARD = 1
12520 REM BANK ACCOUNT INFO ARRAY
12530 REM      POSITION 1 = DOLLAR AMOUNT
12540 REM      POSITION 2 = LAST DEPOSIT DAY ?
12550 REM      POSITION 3 = LAST DEPOSIT YEAR ?
12560 CURRSTAR = 2
12570 GOSUB 13250 : REM PLACE A FRONTIER SYSTEM
12580 CURRSTAR = 3
12590 GOSUB 13250 : REM PLACE A FRONTIER SYSTEM
12600 CURRSTAR = 4
12610 GOSUB 13430 : REM PLACE A UNDER DEVELOPED SYSTEM
12620 FOR CURRSTAR = 5 TO NUMSTARS
12630 ON CURRSTAR - 3 * INT((CURRSTAR-1)/3) GOSUB 13250, 13430, 13520 
12640 NEXT CURRSTAR
12650 REM
12660 REM *** BLOCK #4.3
12670 FOR CURRSTAR=1 TO NUMSTARS : REM LOOP THROUGH STAR SYSTEMS
12680 FOR PRODUCT = 1 TO 6 : REM LOOP THROUGH PRODUCT DATA STORAGE AND
12690 STAR[PRODUCT,CURRSTAR]=0 : REM SET EACH PRODUCT TO 0
12700 NEXT PRODUCT 
12710 IF CURRSTAR > 1 THEN GOTO 12740 : REM RANDOMLY PICK STAR NAMES 
FOR STARS 2 THROUGH ...
12720 I=1
12730 GOTO 12790
12740 RANDOMIZE TIMER
12750 I=4*INT(14*RND(0))+5
12760 FOR J=2 TO CURRSTAR-1
12770 IF I=STAR[8,J] THEN 12740 : REM IF NAME ALREADY USED, TRY AGAIN
12780 NEXT J
12790 STAR[8,CURRSTAR] = I : REM STORE STAR NAME LOOKUP POSITION
12800 STAR[9,CURRSTAR] = 270
12810 STAR[10,CURRSTAR] = CURRYEAR - 1
12820 NEXT CURRSTAR
12830 REM
12840 REM *** BLOCK #4.4
12850 CURRSHIP = 1
12860 REM
12870 PRINT
12880 PRINT
12890 CLS : PRINT "CAPTAINS, NAME YOUR SHIPS (UP TO 6 
LETTERS/BLANKS/NUMBERS)"
12900 FOR I = 1 TO NUMSHIPS/NUMPLAYERS
12910 PRINT
12920 FOR CURRPLAYER = 1 TO NUMPLAYERS
12930 REM
12940 SHIP[1,CURRSHIP] = 0 : REM  PRODUCT 1 UR
12950 SHIP[2,CURRSHIP] = 0 : REM  PRODUCT 2 MET
12960 SHIP[3,CURRSHIP] = 15 : REM PRODUCT 3 HE
12970 SHIP[4,CURRSHIP] = 10 : REM PRODUCT 4 MED
12980 SHIP[5,CURRSHIP] = 10 : REM PRODUCT 5 SOFT
12990 SHIP[6,CURRSHIP] = 0 : REM  PRODUCT 6 GEMS
13000 SHIP[7,CURRSHIP] = 25 REM TOTAL WEIGHT IN CARGO HOLD IN TONS (NOT 
INCLUDING SOFT AND GEMS)
13010 SHIP[8,CURRSHIP] = 0 : REM DESTINATION OF SHIP/STAR SYSTEM NAME 
LOOKUP
13020 SHIP[9,CURRSHIP] = CURRDAY : REM CURRENT DAY
13030 SHIP[10,CURRSHIP] = CURRYEAR : REM CURRENT YEAR
13040 SHIP[11,CURRSHIP] = 5000 : REM CASH ON BOARD SHIP
13050 SHIP[12,CURRSHIP] = 0 : REM ARRIVED LATE AT DESTINATION (0, 1, 2, 
3)
13060 REM
13070 PRINT "   CAPTAIN ";CURRPLAYER; " WHAT DO YOU CHRISTEN YOUR SHIP 
#";I;
13080 INPUT A$ : SHIPNAME$[CURRSHIP]=A$
13090 CURRSHIP = CURRSHIP + 1
13100 REM
13110 NEXT CURRPLAYER
13120 NEXT I
13130 REM *** BLOCK #4.5
13140 FOR CURRPLAYER=1 TO NUMPLAYERS
13150 BANK[1,CURRPLAYER] = 0
13160 BANK[2,CURRPLAYER] = CURRDAY
13170 BANK[3,CURRPLAYER] = CURRYEAR
13180 NEXT CURRPLAYER
13190 CHAIN "MAIN" ,,ALL
13200 REM 
13210 REM ***********************************
13220 REM * GENERATE A FRONTIER STAR SYSTEM *
13230 REM ***********************************
13240 REM 
13250 RANDOMIZE TIMER
13260 X=(RND(0)-.5)*100
13270 Y=50*RND(0)
13320 IF (ABS(X)<25) AND (Y<25) THEN GOTO 13250
13330 F=1
13340 GOSUB 13660
13350 IF F=0 THEN GOTO 13250
13360 STAR[7,CURRSTAR]=0
13370 RETURN
13380 REM
13390 REM *****************************************
13400 REM * GENERATE A UNDERDEVELOPED STAR SYSTEM *
13410 REM *****************************************
13420 REM
13430 E=100
13440 GOSUB 13570
13450 STAR[7,CURRSTAR]=5
13460 RETURN
13470 REM
13480 REM ************************************
13490 REM * GENERATE A DEVELOPED STAR SYSTEM *
13500 REM ************************************
13510 REM
13520 E=50
13530 GOSUB 13570
13540 STAR[7,CURRSTAR]=10
13550 RETURN
13560 REM *** <GENERATE CO-ORDS> GOSUB
13570 RANDOMIZE TIMER
13580 X=(RND(0)-.5)*E
13590 Y=RND(0)*E/2
13600 F=1
13610 GOSUB 13660
13620 IF F=0 THEN GOTO 13570
13630 RETURN
13640 REM *** <TEST STAR CO-ORDS> GOSUB
13650 REM FIRST CONVVERT CO-ORDS TO NEXT HALF-BOARD
13660 ON FLAGHALFBOARD GOTO 13770, 13730, 13710, 13670
13670 Z=X
13680 X=-Y
13690 Y=Z
13700 GOTO 13770
13710 Y=-Y
13720 GOTO 13770
13730 Z=X
13740 X=Y
13750 Y=Z
13760 REM SECOND, TEST PROXIMITY
13770 FOR J=1 TO CURRSTAR-1
13780 REM
13790 IF SQR((X-STAR[11,J])^2+(Y-STAR[12,J])^2) >= MINDIST THEN GOTO 
13820
13800 F=0
13810 RETURN
13820 NEXT J
13830 REM FINALLY, ENTER CO-ORDS AND INCREMENT HALF-BOARD CTR
13840 STAR[11,CURRSTAR]=INT(X)
13850 IF CURRSTAR=1 THEN PRINT CURRSTAR : STOP
13860 STAR[12,CURRSTAR]=INT(Y)
13870 FLAGHALFBOARD = 1 + (FLAGHALFBOARD <=3)* FLAGHALFBOARD 
13880 RETURN
13890 REM *** DATA FOR ECONOMETRIC MODEL FOLLOWS ***
13900 REM MODEL #1
13910 DATA -0.025, -0.05, -0.025, 0, -0.025, -0.025, 0, .1, .1, -0.025, 
.1, 0, .1, .2, .1, .1, -0.025, 0
13920 DATA 1, 1.5, .5, .75, .75, .75, -0.25, -0.25, -0.25, 0, -0.5, .5, 
0, -0.5
13930 DATA 0, .5, 1.5, 0
13940 END
13950 CLS
13960 FOR J = 1 TO 15
13970 PRINT "#";J;
13980 FOR K = 7 TO 12
13990 PRINT K;"=";STAR[K,J];
14000 NEXT K
14010 PRINT
14020 NEXT J
14030 PRINT
14040 RETURN
14050 REM *** LOAD M AND C ARRAYS
14060 RESTORE 13900
14070 FOR J = 1 TO 3
14080 FOR K = 1 TO 6
14090 READ M[K,J]
14100 NEXT K
14110 NEXT J
14120 FOR J = 1 TO 3
14130 FOR K = 1 TO 6